struct PathTracer {
    on_emit:   RayEmitter,
    on_hit:    fn (Ray, Hit, &mut RayState, SurfaceElement, Material, fn (SpectralWeight) -> !) -> (),
    on_shadow: fn (Ray, Hit, &mut RayState, SurfaceElement, Material, fn (Ray, SpectralWeight) -> !) -> (),
    on_bounce: fn (Ray, Hit, &mut RayState, SurfaceElement, Material, fn (Ray, RayState) -> !) -> (),
    on_nonhit: fn (Ray, &mut RayState, fn (SpectralWeight) -> !) -> (),// TODO
}

struct RayState {
    rnd:     RndState,
    contrib: SpectralWeight,
    mis:     f32,
    depth:   i32
}

type RayEmitter = fn (i32, i32, i32, i32, i32) -> (Ray, RayState);

fn @make_ray_state(rnd: RndState, contrib: SpectralWeight, mis: f32, depth: i32) -> RayState {
    RayState {
        rnd: rnd,
        contrib: contrib,
        mis: mis,
        depth: depth
    }
}

fn @make_camera_emitter(scene: Scene, device: Device, iter: i32) -> RayEmitter {
    @ |sample, x, y, width, height| {
        let mut hash = fnv_init();
        hash = fnv_hash(hash, sample as u32);
        hash = fnv_hash(hash, iter as u32);
        hash = fnv_hash(hash, x as u32);
        hash = fnv_hash(hash, y as u32);
        let mut rnd = hash as RndState;
        let kx = 2.0f * (x as f32 + randf(&mut rnd)) / (width  as f32) - 1.0f;
        let ky = 1.0f - 2.0f * (y as f32 + randf(&mut rnd)) / (height as f32);
        let wvl_sample = sample_default_spectral_sample(device.intrinsics, randf(&mut rnd));
        let ray = scene.camera.generate_ray(kx, ky, wvl_sample.wavelength);
        let weight = wvl_sample.weight;
        let state = make_ray_state(rnd, weight, 0.0f, 0);
        (ray, state)
    }
}

fn @make_debug_renderer() -> Renderer {
    @ |scene, device, iter| {
        let on_emit = make_camera_emitter(scene, device, iter);
        let on_shadow = @ |_, _, _, _, _, _| ();
        let on_bounce = @ |_, _, _, _, _, _| ();
        let on_hit = @ |ray, hit, state, surf, mat, accumulate| {
            accumulate(spectral_weight_mulf(make_spectral_weight_one(), -vec3_dot(ray.dir, surf.local.col(2))))
        };
        let on_nonhit = @ |_, _, _| ();

        let path_tracer = PathTracer {
            on_emit:   on_emit,
            on_hit:    on_hit,
            on_shadow: on_shadow,
            on_bounce: on_bounce,
            on_nonhit: on_nonhit
        };

        let mapper = make_tonemapper_srgb();
        device.trace(scene, mapper, path_tracer, 1);
    }
}

fn @make_emission_renderer() -> Renderer {
    @ |scene, device, iter| {
        let on_emit = make_camera_emitter(scene, device, iter);
        let on_shadow = @ |_, _, _, _, _, _| ();
        let on_bounce = @ |_, _, _, _, _, _| ();
        let on_hit = @ |ray, hit, state, surf, mat, accumulate| {
            if mat.is_emissive {
                let out_dir = vec3_neg(ray.dir);
                let emit = mat.emission(out_dir);
                accumulate(spectrum_eval(emit.intensity, ray.wvl))
            }
        };
        let on_nonhit = @ |_, _, _| ();

        let path_tracer = PathTracer {
            on_emit:   on_emit,
            on_hit:    on_hit,
            on_shadow: on_shadow,
            on_bounce: on_bounce,
            on_nonhit: on_nonhit
        };

        let mapper = make_tonemapper_srgb();
        device.trace(scene, mapper, path_tracer, 1);
    }
}

fn @make_whitefurnance_renderer() -> Renderer {
    @ |scene, device, iter| {
        let env = make_d65_illum(1.0f);
        //let env = make_e_illum(1.0f/flt_pi);
        
        let on_emit = make_camera_emitter(scene, device, iter);
        let on_shadow = @ |_, _, _, _, _, _| ();
        let on_bounce = @ |_, _, _, _, _, _| ();
        let on_hit = @ |ray, hit, state, surf, mat, accumulate| {
            if !surf.is_entering { return() }

            let N = surf.local.col(2);
            let in_dir = vec3_neg(ray.dir);
            let out_dir = N;

            //let bsdf = spectral_weight_mulf(state.contrib, 1.0f / flt_pi);
            let bsdf = spectral_weight_mul(state.contrib, mat.bsdf.eval(in_dir, out_dir, ray.wvl));
            //let bsdf = make_spectral_weight_one();
            let contrib = spectral_weight_mul(spectrum_eval(env, ray.wvl), bsdf);
            accumulate(spectral_weight_mulf(contrib, vec3_dot(out_dir, N)))
        };
        let on_nonhit = @ |_, _, _| ();

        let path_tracer = PathTracer {
            on_emit:   on_emit,
            on_hit:    on_hit,
            on_shadow: on_shadow,
            on_bounce: on_bounce,
            on_nonhit: on_nonhit
        };

        let mapper = make_tonemapper_srgb();
        device.trace(scene, mapper, path_tracer, 1);
    }
}

fn @make_path_tracing_renderer(max_path_len: i32, spp: i32)-> Renderer {
    @ |scene, device, iter| {
        let offset = 0.001f;
        let pdf_lightpick = 1.0f / (scene.num_lights as f32);

        let on_emit = make_camera_emitter(scene, device, iter);

        fn @on_shadow( ray: Ray
                     , hit: Hit
                     , state: &mut RayState
                     , surf: SurfaceElement
                     , mat: Material
                     , emit: fn (Ray, SpectralWeight) -> !
                     ) -> () {
            // No shadow rays for specular materials
            if mat.bsdf.is_specular {
                return()
            }

            let rnd = &mut state.rnd;
            // Note: randi() returns random integers, but we only want positive integers here
            let light_id = (randi(rnd) & 0x7FFFFFFF) % scene.num_lights;
            let light = @@(scene.lights)(light_id);
            let light_sample = @@(light.sample_direct)(rnd, surf.point);
            let light_dir = vec3_sub(light_sample.pos, surf.point);
            let vis = vec3_dot(light_dir, surf.local.col(2));

            if vis > 0.0f && light_sample.cos > 0.0f {
                let inv_d = 1.0f / vec3_len(device.intrinsics, light_dir);
                let inv_d2 = inv_d * inv_d;
                let in_dir = vec3_mulf(light_dir, inv_d);
                let out_dir = vec3_neg(ray.dir);

                let pdf_e = if light.has_area { spectral_pdf_sum(mat.bsdf.pdf(in_dir, out_dir, ray.wvl)) } else { 0.0f };
                let pdf_l = light_sample.pdf_area * pdf_lightpick;
                let inv_pdf_l = safe_rcp(pdf_l);

                let cos_e = vis * inv_d;
                let cos_l = light_sample.cos;

                let mis = 1.0f / (1.0f + pdf_e * cos_l * inv_d2 * inv_pdf_l);
                let geom_factor = cos_e * cos_l * inv_d2 * inv_pdf_l;

                let contrib = spectral_weight_mul(spectrum_eval(light_sample.intensity, ray.wvl),
                    spectral_weight_mul(state.contrib, mat.bsdf.eval(in_dir, out_dir, ray.wvl)));
                emit(
                    make_ray(surf.point, light_dir, ray.wvl, offset, 1.0f - offset),
                    spectral_weight_mulf(contrib, geom_factor * mis)
                )
            }
        }

        fn @on_hit( ray: Ray
                  , hit: Hit
                  , state: &mut RayState
                  , surf: SurfaceElement
                  , mat: Material
                  , accumulate: fn (SpectralWeight) -> !
                  ) -> () {
            // Hits on a light source
            if mat.is_emissive && surf.is_entering {
                let out_dir = vec3_neg(ray.dir);
                let emit = mat.emission(out_dir);
                let next_mis = safe_div(state.mis * hit.distance * hit.distance, vec3_dot(out_dir, surf.local.col(2)));
                let mis = 1.0f / (1.0f + next_mis * pdf_lightpick * emit.pdf_area);
                accumulate(spectral_weight_mulf(spectral_weight_mul(state.contrib, spectrum_eval(emit.intensity, ray.wvl)), mis))
            }
        }

        fn @on_bounce( ray: Ray
                     , hit: Hit
                     , state: &mut RayState
                     , surf: SurfaceElement
                     , mat: Material
                     , bounce: fn (Ray, RayState) -> !
                     ) -> () {
            // Russian roulette and maximum depth
            let rr_prob = russian_roulette(state.contrib, 0.75f);
            if state.depth >= max_path_len || randf(&mut state.rnd) >= rr_prob {
                return()
            }

            // Bounce
            let out_dir = vec3_neg(ray.dir);
            let mat_sample = mat.bsdf.sample(&mut state.rnd, out_dir, ray.wvl, false);
            let contrib = spectral_weight_mul(state.contrib, mat_sample.color);
            let mis = if mat.bsdf.is_specular { 0.0f } else { spectral_pdf_mis(mat_sample.pdf) };
            let factor = mat_sample.pdf.hero * rr_prob;
            if factor <= 0.0001f { return() } // Safety check

            bounce(
                make_ray(surf.point, mat_sample.in_dir, ray.wvl, offset, flt_max),
                make_ray_state(state.rnd, spectral_weight_mulf(contrib, mat_sample.cos / factor), mis, state.depth + 1)
            )
        }
        
        let on_nonhit = @ |_, _, _| ();

        let path_tracer = PathTracer {
            on_emit:   on_emit,
            on_hit:    on_hit,
            on_shadow: on_shadow,
            on_bounce: on_bounce,
            on_nonhit: on_nonhit
        };

        let mapper = make_tonemapper_srgb();
        device.trace(scene, mapper, path_tracer, spp);
    }
}
